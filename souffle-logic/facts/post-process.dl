MethodHandleConstantConsMacro(?heap, ?method, "java.lang.invoke.MethodHandle") :-
   MethodHandleConstant_Method(?heap, ?method).

MockHeapConsMacro(?null, ?type),
HeapAllocation_Null(?null) :-
  ?null = "<<null pseudo heap>>",
  Type_null(?type).

MockHeapConsMacro(?heap, ?type),
MainMethodArgArray(?heap) :-
  ?heap = "<<main method array>>",
  ?type = "java.lang.String[]".

MockHeapConsMacro(?heap, ?type),
MainMethodArgArrayContent(?heap) :-
  ?heap = "<<main method array content>>",
  ?type = "java.lang.String".

ApplicationMethod(?method) :-
  Method_DeclaringType(?method, ?class),
  ApplicationClass(?class).

ApplicationVar(?var) :-
  Var_DeclaringMethod(?var, ?method),
  ApplicationMethod(?method).

ConcreteClass(?class) :-
  isClassType(?class),
  !ClassModifier("abstract", ?class).

// Note that this is more than a straightforward import of generated facts into
// a DB relation. It filters out the guessed control types that do not
// correspond to existing types. Relations LayoutControl0 and
// SensitiveLayoutControl0 may also come from XML facts (see android-xml.dl).
#ifdef ANDROID

// Heuristic: if the name is unqualified, it may come from android.view or
// android.widget ("Android Programming: The Big Nerd Ranch Guide", chapter 32).
#define QUAL_CONTROL_TYPE(t, qType) \
  (qType = t ; qType = cat("android.view.", t) ; qType = cat("android.widget.", t))

LayoutControl(?id, ?type, ?parent) :-
  LayoutControl0(?id, ?type, ?parent),
  QUAL_CONTROL_TYPE(?type, ?qType),
  isType(?qType).

SensitiveLayoutControl(?id, ?type, ?parent) :-
  SensitiveLayoutControl0(?id, ?type, ?parent),
  QUAL_CONTROL_TYPE(?type, ?qType),
  isType(?qType).

#endif // ANDROID
