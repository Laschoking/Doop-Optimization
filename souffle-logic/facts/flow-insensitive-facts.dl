//-----------------------------------------------------------------------------
// [Flow-insensitive Schema Layer]
//
// The following section contains a slightly modified schema that hides the
// underlying instruction entities, which are irrelevant to flow-insensitive
// analyses.
//
// The basic pattern is that, for each instruction entity, we introduce another
// predicate that joins all of its properties (encoded as functional
// predicates) but does not contain the instruction itself.
//
// Note that this pattern applies only to a subset of instructions that is
// important for flow-insensitive analysis.
//-----------------------------------------------------------------------------

.decl Throw(?insn:Throw_Insn, ?var:Var)

.decl LoadInstanceField(?base:Var, ?sig:Field, ?to:Var, ?inmethod:Method)

.decl StoreInstanceField(?from:Var, ?base:Var, ?signature:Field, ?inmethod:Method)

.decl LoadStaticField(?sig:Field, ?to:Var, ?inmethod:Method)

.decl StoreStaticField(?from:Var, ?signature:Field, ?inmethod:Method)

.decl LoadArrayIndex(?base:Var, ?to:Var, ?inmethod:Method)

.decl StoreArrayIndex(?from:Var, ?base:Var, ?inmethod:Method)

.decl AssignCast(?type:Type, ?from:Var, ?to:Var, ?inmethod:Method)

.decl AssignLocal(?from:Var, ?to:Var, ?inmethod:Method)

.decl AssignNull(?to:Var, ?inmethod:Method)

.decl AssignHeapAllocation(?heap:HeapAllocation, ?to:Var, ?inmethod:Method)

// Note: a Jimple method can have multiple return variables
.decl ReturnVar(?var:Var, ?method:Method)

.decl VirtualMethodInvocation(?invocation:MethodInvocation, ?signature:Method, ?inmethod:Method)

.decl SuperMethodInvocation(?invocation:MethodInvocation, ?signature:Method, ?inmethod:Method)

.decl StaticMethodInvocation(?invocation:MethodInvocation, ?signature:Method, ?inmethod:Method)


Throw(?insn, ?var) :-
	isThrow_Insn(?insn),
	Throw_Var(?insn, ?var).

LoadInstanceField(?base, ?sig, ?to, ?inmethod) :-
	isLoadInstanceField_Insn(?insn),
	Instruction_Method(?insn, ?inmethod),
	LoadInstanceField_Base(?insn, ?base),
	FieldInstruction_Signature(?insn, ?sig),
	LoadInstanceField_To(?insn, ?to).

StoreInstanceField(?from, ?base, ?sig, ?inmethod) :-
	isStoreInstanceField_Insn(?insn),
	Instruction_Method(?insn, ?inmethod),
	StoreInstanceField_From(?insn, ?from),
	StoreInstanceField_Base(?insn, ?base),
	FieldInstruction_Signature(?insn, ?sig).

LoadStaticField(?sig, ?to, ?inmethod) :-
	isLoadStaticField_Insn(?insn),
	Instruction_Method(?insn, ?inmethod),
	FieldInstruction_Signature(?insn, ?sig),
	LoadStaticField_To(?insn, ?to).

StoreStaticField(?from, ?sig, ?inmethod) :-
	isStoreStaticField_Insn(?insn),
	Instruction_Method(?insn, ?inmethod),
	StoreStaticField_From(?insn, ?from),
	FieldInstruction_Signature(?insn, ?sig).

LoadArrayIndex(?base, ?to, ?inmethod) :-
	isLoadArrayIndex_Insn(?insn),
	Instruction_Method(?insn, ?inmethod),
	LoadArrayIndex_Base(?insn, ?base),
	LoadArrayIndex_To(?insn, ?to).

StoreArrayIndex(?from, ?base, ?inmethod) :-
	isStoreArrayIndex_Insn(?insn),
	Instruction_Method(?insn, ?inmethod),
	StoreArrayIndex_From(?insn, ?from),
	StoreArrayIndex_Base(?insn, ?base).

AssignCast(?type, ?from, ?to, ?inmethod) :-
	isAssignCast_Insn(?insn),
	Instruction_Method(?insn, ?inmethod),
	AssignCast_From(?insn, ?from),
	AssignInstruction_To(?insn, ?to),
	AssignCast_Type(?insn, ?type).

AssignLocal(?from, ?to, ?inmethod) :-
	isAssignLocal_Insn(?insn),
	AssignInstruction_To(?insn, ?to),
	Instruction_Method(?insn, ?inmethod),
	AssignLocal_From(?insn, ?from).

AssignNull(?to, ?inmethod) :-
	isAssignNull_Insn(?insn),
	AssignInstruction_To(?insn, ?to),
	Instruction_Method(?insn, ?inmethod).

AssignHeapAllocation(?heap, ?to, ?inmethod) :-
	isAssignHeapAllocation_Insn(?insn),
	Instruction_Method(?insn, ?inmethod),
	AssignHeapAllocation_Heap(?insn, ?heap),
	AssignInstruction_To(?insn, ?to).

ReturnVar(?var, ?method) :-
	isReturnNonvoid_Insn(?insn),
	Instruction_Method(?insn, ?method),
	ReturnNonvoid_Var(?insn, ?var).

ReturnVar(?var, ?method) :-
	NativeReturnVar(?var, ?method).

VirtualMethodInvocation(?invocation, ?signature, ?inmethod) :-
	isVirtualMethodInvocation_Insn(?invocation),
	Instruction_Method(?invocation, ?inmethod),
	MethodInvocation_Method(?invocation, ?signature).

StaticMethodInvocation(?invocation, ?signature, ?inmethod) :-
	isStaticMethodInvocation_Insn(?invocation),
	Instruction_Method(?invocation, ?inmethod),
	MethodInvocation_Method(?invocation, ?signature).

SuperMethodInvocation(?invocation, ?signature, ?inmethod) :-
	isSuperMethodInvocation_Insn(?invocation),
	Instruction_Method(?invocation, ?inmethod),
	MethodInvocation_Method(?invocation, ?signature).

VirtualMethodInvocation_SimpleName(?invocation, ?simplename),
VirtualMethodInvocation_Descriptor(?invocation, ?descriptor) :-
	isVirtualMethodInvocation_Insn(?invocation),
	MethodInvocation_Method(?invocation, ?signature),
	Method_SimpleName(?signature, ?simplename),
	Method_Descriptor(?signature, ?descriptor).

StaticMethodInvocation_SimpleName(?invocation, ?simplename),
StaticMethodInvocation_Descriptor(?invocation, ?descriptor) :-
	isStaticMethodInvocation_Insn(?invocation),
	MethodInvocation_Method(?invocation, ?signature),
	Method_SimpleName(?signature, ?simplename),
	Method_Descriptor(?signature, ?descriptor).

SpecialMethodInvocation_SimpleName(?invocation, ?simplename),
SpecialMethodInvocation_Descriptor(?invocation, ?descriptor) :-
	isSpecialMethodInvocation_Insn(?invocation),
	MethodInvocation_Method(?invocation, ?signature),
	Method_SimpleName(?signature, ?simplename),
	Method_Descriptor(?signature, ?descriptor).

SuperMethodInvocation_SimpleName(?invocation, ?simplename),
SuperMethodInvocation_Descriptor(?invocation, ?descriptor) :-
	isSuperMethodInvocation_Insn(?invocation),
	MethodInvocation_Method(?invocation, ?signature),
	Method_SimpleName(?signature, ?simplename),
	Method_Descriptor(?signature, ?descriptor).

.decl MethodInvocation_SimpleName(?invocation:Instruction, ?simpleName:symbol)

MethodInvocation_SimpleName(?invocation, ?simplename) :-
	VirtualMethodInvocation_SimpleName(?invocation, ?simplename).
MethodInvocation_SimpleName(?invocation, ?simplename) :-
	SpecialMethodInvocation_SimpleName(?invocation, ?simplename).
MethodInvocation_SimpleName(?invocation, ?simplename) :-
	StaticMethodInvocation_SimpleName(?invocation, ?simplename).
MethodInvocation_SimpleName(?invocation, ?simplename) :-
	SuperMethodInvocation_SimpleName(?invocation, ?simplename).
