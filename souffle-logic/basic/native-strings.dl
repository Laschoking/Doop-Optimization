.decl PossibleNativeCodeTargetMethod(?method:Method, ?function:symbol, ?file:symbol)

PossibleNativeCodeTargetMethod(?method, ?function, ?file) :-
  _NativeMethodTypeCandidate(?file, ?function, ?descriptor, _),
  _NativeNameCandidate(?file, ?function, ?name, _),
  Method_SimpleName(?method, ?name),
  Method_JVMDescriptor(?method, ?descriptor).

// PossibleNativeCodeTargetMethod(?method, ?function, ?file) :-
//   _NativeMethodTypeCandidate(?file, ?function, ?descriptor, ?offset1),
//   _NativeNameCandidate(?file, ?function, ?name, ?offset2),
//   Method_SimpleName(?method, ?name),
//   Method_JVMDescriptor(?method, ?descriptor),
//   (?offset1 - ?offset2) <= 5,
//   (?offset1 - ?offset2) >= -5.

// // Crude over-approximation for strings used in unknown positions:
// // assume matching methods are called from any function.
// PossibleNativeCodeTargetMethod(?method, ?function, ?file) :-
//   _NativeMethodTypeCandidate(?file, "-", ?descriptor, _),
//   _NativeNameCandidate(?file, "-", ?name, _),
//   _NativeLibEntryPoint(?file, ?function, _),
//   Method_SimpleName(?method, ?name),
//   Method_JVMDescriptor(?method, ?descriptor).

.decl PossibleNativeCodeTargetMethodLocalized(?method:Method, ?function:symbol, ?file:symbol)

PossibleNativeCodeTargetMethodLocalized(?method, ?function, ?file) :-
  PossibleNativeCodeTargetMethod(?method, ?function, ?file),
  ?function != "-".

.decl OverloadedJNIMethod(?javaMethod:Method, ?type:ReferenceType, ?name:symbol, ?descriptor:symbol)

OverloadedJNIMethod(?javaMethod, ?type, ?name, ?descriptor) :-
  Method_Modifier("native", ?javaMethod),
  Method_SimpleName(?javaMethod, ?name),
  Method_Descriptor(?javaMethod, ?descriptor),
  Method_DeclaringType(?javaMethod, ?type),
  MethodLookup(?name, _, ?type, ?method1),
  MethodLookup(?name, _, ?type, ?method2),
  ?method1 != ?method2.

.decl JNIMethod_NativeId(?javaMethod:Method, ?nativeId:symbol)
.output JNIMethod_NativeId

// Non-overloaded native method (default, always generated).
JNIMethod_NativeId(?javaMethod, ?nativeId) :-
  _NativeMethodId(?javaMethod, ?nativeId),
  isMethod(?javaMethod).

// Overloaded native method, use computed name as prefix (see JNI spec).
JNIMethod_NativeId(?javaMethod, ?nativeId) :-
  OverloadedJNIMethod(?javaMethod, _, _, ?descriptor),
  _NativeMethodId(?javaMethod, ?nativeIdBase),
  ?nativeId = cat(?nativeIdBase, cat("__", ?descriptor)).

// Call-graph edges from native methods. This only captures direct call-graph
// edges from native methods; if a native method calls other native code to do
// the call, that behavior will be missed.

.decl CallGraphEdgeFromNativeMethod(?targetMethod:Method, ?nativeMethod:Method, ?function:symbol, ?file:symbol)
.output CallGraphEdgeFromNativeMethod

CallGraphEdgeFromNativeMethod(?targetMethod, ?nativeMethod, ?function, ?file) :-
  PossibleNativeCodeTargetMethod(?targetMethod, ?function, ?file),
  JNIMethod_NativeId(?nativeMethod, ?function).

// Filtered CallGraphEdgeFromNativeMethod, in the "application" part.
.decl AppCallGraphEdgeFromNativeMethod(?targetMethod:Method, ?nativeMethod:Method)
.output AppCallGraphEdgeFromNativeMethod

AppCallGraphEdgeFromNativeMethod(?targetMethod, ?nativeMethod) :-
  CallGraphEdgeFromNativeMethod(?targetMethod, ?nativeMethod, _, _),
  ApplicationMethod(?targetMethod),
  ApplicationMethod(?nativeMethod).

// Native allocations detected by the native scanner.
.decl NativeAllocation(?constructor:Method, ?function:symbol, ?file:symbol, ?type:ReferenceType)
NativeAllocation(?constructor, ?function, ?file, ?type) :-
  PossibleNativeCodeTargetMethod(?constructor, ?function, ?file),
  ClassConstructor(?constructor, ?type).

