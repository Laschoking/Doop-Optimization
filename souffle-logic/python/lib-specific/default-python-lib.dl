

//----------------------------------------------------------------------------------------------------------------------
//                                                  FUNCTION DECLARATIONS
//----------------------------------------------------------------------------------------------------------------------


isModule("<BUILTIN_SCOPE>").

isModule("<random>").
isModeledModule("<random>").

VarPointsTo(?hctx, "<None>", ?ctx, "<BUILTIN_SCOPE>/noneDefVar"):-
  isImmutableContext(?ctx),
  isImmutableHContext(?hctx).

ModeledFunction("<range>", "range", "<BUILTIN_SCOPE>").

FormalParam( 0, "startStop", "<range>", "<range>/mockParam1").
FormalParam( 1, "stop", "<range>", "<range>/mockParam2").
FormalParam( 2, "step", "<range>", "<range>/mockParam3").
FormalParam_DefaultValForName("<range>", "startStop", "<BUILTIN_SCOPE>/noneDefVar").
FormalParam_DefaultValForName("<range>", "stop", "<BUILTIN_SCOPE>/noneDefVar").
FormalParam_DefaultValForName("<range>", "step", "<BUILTIN_SCOPE>/noneDefVar").

ModeledFunction("<len>", "len", "<BUILTIN_SCOPE>").

FormalParam( 0, "s", "<len>", "<len>/mockParam1").

ModeledFunction("<random.randint>", "randint", "<random>").

//----------------------------------------------------------------------------------------------------------------------
//                                                  GENERAL OP MODELING
//----------------------------------------------------------------------------------------------------------------------


.decl BuiltInOp_NameToVal(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?operationVal:Value, ?name:symbol, ?namedValHctx:configuration.HContext, ?namedVal:Value)
.output BuiltInOp_NameToVal

.decl BuiltInOpProducesOutput(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?operationVal:Value)
.output BuiltInOpProducesOutput


configuration.RecordContextRequest(?ctx, ?operationVal, ?var) :-
  BuiltInOpProducesOutput(?ctx, ?insn, ?operationVal),
  AssignInstruction_To(?insn, ?var).

VarPointsTo(?hctx, ?operationVal, ?ctx, ?var):-
  configuration.RecordContextResponse(?ctx, ?operationVal, ?var, ?hctx),
  BuiltInOpProducesOutput(?ctx, ?insn, ?operationVal),
  AssignInstruction_To(?insn, ?var).


//----------------------------------------------------------------------------------------------------------------------
//                                                  RANGE OP
//----------------------------------------------------------------------------------------------------------------------

.decl RangeOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?rangeOp:Value)
.output RangeOp

.decl RangeOpOneArg(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?rangeOp:Value)
.output RangeOpOneArg

#define RangeValMacro(op,fun, index, val1, val2, val3) cat("<range ", cat(fun, cat("/", cat(op, cat( to_string(index), cat(val1, cat("+", cat(val2, cat("+", cat(val3, ">"))))))))))


BuiltInOp_NameToVal(?ctx, ?insn, ?rangeOp, "startStop", ?startStopHctx, ?startStop),
BuiltInOp_NameToVal(?ctx, ?insn, ?rangeOp, "stop", ?stopHctx, ?stop),
BuiltInOp_NameToVal(?ctx, ?insn, ?rangeOp, "step", ?stepHctx, ?step),
RangeOp(?ctx, ?insn, ?rangeOp):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<range>"),
  ResolvedActualParamValue(?ctx, ?insn, "<range>", "startStop", ?startStopHctx, ?startStop),
  ResolvedActualParamValue(?ctx, ?insn, "<range>", "stop", ?stopHctx, ?stop),
  ResolvedActualParamValue(?ctx, ?insn, "<range>", "step", ?stepHctx, ?step),
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?rangeOp = RangeValMacro("range", ?fun, ?index, ?startStop, ?stop, ?step).

RangeOpOneArg(?ctx, ?insn, ?rangeOp),
BuiltInOpProducesOutput(?ctx, ?insn, ?rangeOp):-
  RangeOp(?ctx, ?insn, ?rangeOp),
  BuiltInOp_NameToVal(?ctx, ?insn, ?rangeOp, "startStop", _, ?startStop),
  ?startStop != "<None>",
  BuiltInOp_NameToVal(?ctx, ?insn, ?rangeOp, "stop", _, "<None>"),
  BuiltInOp_NameToVal(?ctx, ?insn, ?rangeOp, "step", _, "<None>").


IterableObjectLength(?hctx, ?rangeOp, ?startStop):-
  RangeOpOneArg(?ctx, ?insn, ?rangeOp),
  BuiltInOp_NameToVal(?ctx, ?insn, ?rangeOp, "startStop", _, ?startStop),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?rangeOp, ?ctx, ?var).



//----------------------------------------------------------------------------------------------------------------------
//                                                  LEN OP
//----------------------------------------------------------------------------------------------------------------------

.decl LenOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?lenOp:Value, ?inValHctx:configuration.HContext, ?inVal:Value)
.output LenOp

#define LenValMacro(op,fun, index, inval) cat("<len ", cat(fun, cat("/", cat(op, cat( to_string(index), cat(inval, ">"))))))


LenOp(?ctx, ?insn, ?lenOp, ?inValHctx, ?inVal):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<len>"),
  ResolvedActualParamValue(?ctx, ?insn, "<len>", "s", ?inValHctx, ?inVal),
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?lenOp = LenValMacro("range", ?fun, ?index, ?inVal).

VarPointsTo(?hctx, ?val, ?ctx, ?var):-
  LenOp(?ctx, ?insn, _, ?inValHctx, ?inVal),
  AssignInstruction_To(?insn, ?var),
  TensorLikeObjectShapeListContent(?inValHctx, ?inVal, 0, ?val),
  isImmutableHContext(?hctx).           //Maybe Review this


//----------------------------------------------------------------------------------------------------------------------
//                                                  RANDOM RANDINT
//----------------------------------------------------------------------------------------------------------------------

Value_Num("<num-constant(int):0>", "0").
isIntConstant("0").

VarPointsTo(?hctx, ?mockVal, ?ctx, ?var):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<random.randint>"),
  AssignInstruction_To(?insn, ?var),
  isImmutableHContext(?hctx),
  ?mockVal = "<num-constant(int):0>".


//----------------------------------------------------------------------------------------------------------------------
//                                                  ARGPARSE
//----------------------------------------------------------------------------------------------------------------------

.decl ArgParseValueDefaultNamespace(?argVal:Value, ?argHctx:configuration.HContext, ?nameSpaceVal:Value)
.output ArgParseValueDefaultNamespace

.decl RawString_RemoveDashes(?before:StringConstant, ?after:symbol)
.output RawString_RemoveDashes

RawString_RemoveDashes(?before, ?before):-
  Value_String(_, ?before),
  !contains("-" ,?before).

RawString_RemoveDashes(?before, ?after):-
  Value_String(_, ?before),
  contains("-" ,?before),
  ?after = substr(?before, 1, strlen(?before)),
  !contains("-" ,?after).

RawString_RemoveDashes(?before, ?after):-
  Value_String(_, ?before),
  contains("--" ,?before),
  ?after = substr(?before, 2, strlen(?before)),
  !contains("-" ,?after).

isModule("<argparse>").
isModeledModule("<argparse>").

VarPointsTo(?hctx, "<None>", ?ctx, "<argparse>/noneDefVal") :-
  isImmutableContext(?ctx),
  isImmutableHContext(?hctx).

ModeledClass("<argparse.ArgumentParser>", "ArgumentParser", "<argparse>").

ModeledMethod("<argparse.ArgumentParser.__init__>", "__init__", "<argparse.ArgumentParser>", "<argparse>").

ModeledMethod("<argparse.ArgumentParser.add_argument>", "add_argument", "<argparse.ArgumentParser>", "<argparse>").
FormalParam(0, "self", "<argparse.ArgumentParser.add_argument>", "<argparse.ArgumentParser.add_argument>/mockParam1").
FormalParam(1, "nameOrFlags", "<argparse.ArgumentParser.add_argument>", "<argparse.ArgumentParser.add_argument>/mockParam2").
FormalParam(2, "action", "<argparse.ArgumentParser.add_argument>", "<argparse.ArgumentParser.add_argument>/mockParam3").
FormalParam(3, "nargs", "<argparse.ArgumentParser.add_argument>", "<argparse.ArgumentParser.add_argument>/mockParam4").
FormalParam(4, "const", "<argparse.ArgumentParser.add_argument>", "<argparse.ArgumentParser.add_argument>/mockParam5").
FormalParam(5, "default", "<argparse.ArgumentParser.add_argument>", "<argparse.ArgumentParser.add_argument>/mockParam6").
FormalParam(6, "type", "<argparse.ArgumentParser.add_argument>", "<argparse.ArgumentParser.add_argument>/mockParam7").

FormalParam_DefaultValForName("<argparse.ArgumentParser.add_argument>", ?name, "<argparse>/noneDefVal"):-
  FormalParam(?index, ?name, "<argparse.ArgumentParser.add_argument>", _),
  ?index > 1.

ModeledMethod("<argparse.ArgumentParser.parse_args>", "parse_args", "<argparse.ArgumentParser>", "<argparse>").
FormalParam(0, "self", "<argparse.ArgumentParser.parse_args>", "<argparse.ArgumentParser.parse_args>/mockParam1").
FormalParam(1, "args", "<argparse.ArgumentParser.parse_args>", "<argparse.ArgumentParser.parse_args>/mockParam2").
FormalParam(2, "namespace", "<argparse.ArgumentParser.parse_args>", "<argparse.ArgumentParser.parse_args>/mockParam3").

FormalParam_DefaultValForName("<argparse.ArgumentParser.parse_args>", "args", "<argparse>/noneDefVal").
FormalParam_DefaultValForName("<argparse.ArgumentParser.parse_args>", "namespace", "<argparse>/noneDefVal").

ArgParseValueDefaultNamespace(?val, ?hctx, ?namespaceVal):-
  Value_Type(?val, "<argparse.ArgumentParser>"),
  isClassInstanceAllocation(?val),
  VarPointsTo(?hctx, ?val, _, _),
  ?namespaceVal = cat(cat("<default-namespace-of", ?val),">").

InstanceFieldPointsTo(?hctx, ?value, ?signature, ?argParseHctx, ?namespaceVal):-
  ArgParseValueDefaultNamespace(?argParseInstanceVal, ?argParseHctx, ?namespaceVal),
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<argparse.ArgumentParser.add_argument>"),
  ResolvedActualParamValue(?ctx, ?insn, "<argparse.ArgumentParser.add_argument>", "self", ?argParseHctx, ?argParseInstanceVal),
  ResolvedActualParamValue(?ctx, ?insn, "<argparse.ArgumentParser.add_argument>", "nameOrFlags", _, ?nameVal),
  ResolvedActualParamValue(?ctx, ?insn, "<argparse.ArgumentParser.add_argument>", "default", ?hctx, ?value),
  Value_String(?nameVal, ?nameStr),
  RawString_RemoveDashes(?nameStr, ?signature).

VarPointsTo(?argParseHctx, ?namespaceVal, ?ctx, ?var):-
  ArgParseValueDefaultNamespace(?argParseInstanceVal, ?argParseHctx, ?namespaceVal),
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<argparse.ArgumentParser.parse_args>"),
  ResolvedActualParamValue(?ctx, ?insn, "<argparse.ArgumentParser.parse_args>", "self", ?argParseHctx, ?argParseInstanceVal),
  AssignInstruction_To(?insn, ?var).
