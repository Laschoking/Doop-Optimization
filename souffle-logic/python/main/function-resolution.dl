.decl FunctionInvResolvesTo(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?val:Value, ?hctx:configuration.HContext, ?fun:Function)
.decl ResolvedFunctionActualParam(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function, ?formalParamVar:Var, ?actualParamVar:Var)
.decl ResolvedFunctionHasActualKeywordParamForIndex(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function, ?formalParamIndex:number, ?actualParamVar:Var)
.decl ResolvedFunctionHasActualPositionalParamForIndex(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function, ?formalParamIndex:number, ?actualParamVar:Var)
.decl ResolvedFunctionNoActualKeywordParamForIndex(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function, ?formalParamIndex:number)
.decl ResolvedFunctionNoActualPositionalParamForIndex(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function, ?formalParamIndex:number)
.decl ResolvedFunctionHasActualParamForIndex(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function, ?formalParamIndex:number)
.decl ResolvedFunctionMissingActualOrDefaultParamForIndex(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function, ?formalParamIndex:number)
.decl ResolvedFunctionMissingActualParamForIndex(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function, ?formalParamIndex:number)
.decl ResolvedFunctionHasKeywordAndPositionalActualParamForIndex(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function, ?formalParamIndex:number)
.decl ResolvedFunctionHasActualParamUptoIndex(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function, ?formalParamIndex:number)
.decl ResolvedFunctionHasAllRequiredParams(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?tofunction:Function)


.output ResolvedFunctionActualParam
.output ResolvedFunctionHasAllRequiredParams
.output ResolvedFunctionHasKeywordAndPositionalActualParamForIndex
.output ResolvedFunctionMissingActualOrDefaultParamForIndex
.output ResolvedFunctionMissingActualParamForIndex


configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?tofunction):-
  isModeledFunction(?tofunction),
  FunctionInvResolvesTo(?callerCtx, ?invo, ?value, ?hctx, ?tofunction).

configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?tofunction):-
  !isModeledFunction(?tofunction),
  ResolvedFunctionHasAllRequiredParams(?callerCtx, ?invo, ?tofunction),
  FunctionInvResolvesTo(?callerCtx, ?invo, ?value, ?hctx, ?tofunction).

//Unbound Functions/Methods

ResolvedFunctionHasActualPositionalParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex, ?actual):-
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, _, ?tofunction),
  isUnboundMethodOrFunctionObjectAllocation(?baseValue),
  FormalParam(?formalIndex, _, ?tofunction, _),
  ActualPositionalParam(?formalIndex, ?invo, ?actual).

ResolvedFunctionNoActualPositionalParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex):-
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, _, ?tofunction),
  isUnboundMethodOrFunctionObjectAllocation(?baseValue),
  FormalParam(?formalIndex, _, ?tofunction, _),
  !ActualPositionalParam(?formalIndex, ?invo, _).

//Bound Methods

ResolvedFunctionHasActualPositionalParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex, ?actual):-
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, _, ?tofunction),
  isBoundMethodObjectAllocation(?baseValue),
  FormalParam(?formalIndex, _, ?tofunction, _),
  ActualPositionalParam(?formalIndex - 1, ?invo, ?actual).

ResolvedFunctionNoActualPositionalParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex):-
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, _, ?tofunction),
  isBoundMethodObjectAllocation(?baseValue),
  FormalParam(?formalIndex, _, ?tofunction, _),
  !ActualPositionalParam(?formalIndex - 1, ?invo, _).

ResolvedFunctionHasActualKeywordParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex, ?actual):-
  FunctionInvResolvesTo(?callerCtx, ?invo, _, _, ?tofunction),
  FormalParam(?formalIndex, ?paramName, ?tofunction, _),
  ActualKeywordParam(_, ?invo, ?paramName, ?actual).

ResolvedFunctionNoActualKeywordParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex):-
  FunctionInvResolvesTo(?callerCtx, ?invo, _, _, ?tofunction),
  FormalParam(?formalIndex, ?paramName, ?tofunction, _),
  !ActualKeywordParam(_, ?invo, ?paramName, _).

ResolvedFunctionActualParam(?callerCtx, ?invo, ?tofunction, ?formal, ?actual):-
  ResolvedFunctionHasActualKeywordParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex, ?actual),
  FormalParam(?formalIndex, _, ?tofunction, ?formal),
  ResolvedFunctionNoActualPositionalParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex).

ResolvedFunctionActualParam(?callerCtx, ?invo, ?tofunction, ?formal, ?actual):-
  ResolvedFunctionHasActualPositionalParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex, ?actual),
  FormalParam(?formalIndex, _, ?tofunction, ?formal),
  ResolvedFunctionNoActualKeywordParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex).

ResolvedFunctionHasKeywordAndPositionalActualParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex):-
  ResolvedFunctionHasActualPositionalParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex, _),
  ResolvedFunctionHasActualKeywordParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex, _).

ResolvedFunctionMissingActualParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex):-
  ResolvedFunctionNoActualPositionalParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex),
  ResolvedFunctionNoActualKeywordParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex).

ResolvedFunctionMissingActualOrDefaultParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex):-
  ResolvedFunctionMissingActualParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex),
  !FormalParam_DefaultValForIndex(?tofunction, ?formalIndex, _).



ResolvedFunctionHasActualParamForIndex(?callerCtx, ?invo, ?tofunction, ?formalIndex):-
  ResolvedFunctionActualParam(?callerCtx, ?invo, ?tofunction, ?formal, _),
  FormalParam(?formalIndex, _, ?tofunction, ?formal).

ResolvedFunctionHasActualParamForIndex(?callerCtx, ?invo, ?tofunction, 0):-
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, _, ?tofunction),
  isBoundMethodObjectAllocation(?baseValue).

ResolvedFunctionHasActualParamForIndex(?callerCtx, ?invo, ?tofunction, 0):-
  FunctionInvResolvesTo(?callerCtx, ?invo, _, _, ?tofunction),
  isMethod(?tofunction),
  Function_SimpleName(?tofunction, "__init__").

ResolvedFunctionHasActualParamUptoIndex(?callerCtx, ?invo, ?tofunction, 0):-
  ResolvedFunctionHasActualParamForIndex(?callerCtx, ?invo, ?tofunction, 0).

ResolvedFunctionHasActualParamUptoIndex(?callerCtx, ?invo, ?tofunction, ?index):-
  ResolvedFunctionHasActualParamForIndex(?callerCtx, ?invo, ?tofunction, ?index),
  ResolvedFunctionHasActualParamUptoIndex(?callerCtx, ?invo, ?tofunction, ?index - 1).

ResolvedFunctionHasAllRequiredParams(?callerCtx, ?invo, ?tofunction):-
  ResolvedFunctionHasActualParamUptoIndex(?callerCtx, ?invo, ?tofunction, ?index),
  Function_NumOfRequiredParams(?tofunction, ?index + 1).


FunctionInvResolvesTo(?callerCtx, ?invo, ?value, ?hctx, ?tofunction):-
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base),
  FunctionInvocation_Base(?invo, ?base),
  HeapAllocation_Type(?value, ?tofunction),
  isFunction(?tofunction).

FunctionInvResolvesTo(?callerCtx, ?invo, ?toFunVal, ?toFunHctx, ?tofunction):-
  FunctionInvocation_Base(?invo, ?base),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base),
  isClassObjectAllocation(?value),
  InstanceFieldPointsTo(?toFunHctx, ?toFunVal, "__init__", ?hctx, ?value),
  HeapAllocation_Type(?toFunVal, ?tofunction),
  isFunction(?tofunction).


isContext(?calleeCtx),
CallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?tofunction):-
  configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value, ?tofunction),
  configuration.ContextResponse(?callerCtx, ?hctx, ?invocation, ?value, ?calleeCtx).

.decl ResolvedActualParamValue(?callerCtx: configuration.Context, ?invo:Instruction, ?resolvedFunction:Function, ?paramName:symbol, ?hctx:configuration.HContext, ?paramValue:Value)

//FOR NON BOUND METHODS AND FOR FUNCTION CALLS

// Interprocedural Assign
VarPointsTo(?hctx, ?value, ?calleeCtx, ?formal),
ResolvedActualParamValue(?callerCtx, ?invo, ?tofunction, ?paramName, ?hctx, ?value):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, ?basehctx, ?tofunction),
  isUnboundMethodOrFunctionObjectAllocation(?baseValue),
  FormalParam(?paramIndex, ?paramName, ?tofunction, ?formal),
  (
    ActualPositionalParam(?paramIndex, ?invo, ?actual);
    ActualKeywordParam(_, ?invo, ?paramName, ?actual)
  ),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?actual).

// Default arguments
VarPointsTo(?hctx, ?value, ?calleeCtx, ?formal),
ResolvedActualParamValue(?callerCtx, ?invo, ?tofunction, ?paramName, ?hctx, ?value):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, ?basehctx, ?tofunction),
  isUnboundMethodOrFunctionObjectAllocation(?baseValue),
  FormalParam(?paramIndex, ?paramName, ?tofunction, ?formal),
  !(
    ActualPositionalParam(?paramIndex, ?invo, _);
    ActualKeywordParam(_, ?invo, ?paramName, _)
  ),
  FormalParam_DefaultValForName(?tofunction, ?paramName, ?default),
  VarPointsTo(?hctx,  ?value, _, ?default).       //TODO: REVIEW this at some point

//FOR BOUND METHOD CALLS
VarPointsTo(?selfHctx, ?selfVal, ?calleeCtx, ?formal),
ResolvedActualParamValue(?callerCtx, ?invo, ?tofunction, ?paramName, ?selfHctx, ?selfVal):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, ?basehctx, ?tofunction),
  isBoundMethodObjectAllocation(?baseValue),
  BoundMethodAllocationSelfArgument(?basehctx, ?baseValue, ?selfHctx, ?selfVal),
  FormalParam(0, ?paramName, ?tofunction, ?formal).

// Interprocedural Assign
VarPointsTo(?hctx, ?value, ?calleeCtx, ?formal),
ResolvedActualParamValue(?callerCtx, ?invo, ?tofunction, ?paramName, ?hctx, ?value):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, ?basehctx, ?tofunction),
  isBoundMethodObjectAllocation(?baseValue),
  FormalParam(?paramIndex, ?paramName, ?tofunction, ?formal),
  (
    ActualPositionalParam(?paramIndex - 1, ?invo, ?actual);
    ActualKeywordParam(_, ?invo, ?paramName, ?actual)
  ),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?actual).

// Default arguments
VarPointsTo(?hctx, ?value, ?calleeCtx, ?formal),
ResolvedActualParamValue(?callerCtx, ?invo, ?tofunction, ?paramName, ?hctx, ?value):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, ?basehctx, ?tofunction),
  isBoundMethodObjectAllocation(?baseValue),
  FormalParam(?paramIndex, ?paramName, ?tofunction, ?formal),
  !(
    ActualPositionalParam(?paramIndex - 1, ?invo, _);
    ActualKeywordParam(_, ?invo, ?paramName, _)
  ),
  FormalParam_DefaultValForName(?tofunction, ?paramName, ?default),
  VarPointsTo(?hctx,  ?value, _, ?default).       //TODO: REVIEW this at some point

//VarPointsTo(?hctx, ?value, ?callerCtx, ?return):-
//  CallGraphEdge(?callerCtx, ?invo, _, ?tofunction),
//  ReachableContext(?callerCtx, ?function),
//  Instruction_Function(?invo, ?function),
//  AssignInstruction_To(?invo, ?return),
//  Instruction_Function(?retInsn, ?tofunction),
//  !(isReturnInstruction(?retInsn)),
//  isNoneValue(?value),
//  isImmutableHContext(?hctx).

VarPointsTo(?hctx, ?value, ?callerCtx, ?return):-
  CallGraphEdge(?callerCtx, ?invo, _, ?tofunction),
  ReachableContext(?callerCtx, ?function),
  Instruction_Function(?invo, ?function),
  AssignInstruction_To(?invo, ?return),
  isReturnNone_Insn(?retInsn),
  Instruction_Function(?retInsn, ?tofunction),
  isNoneValue(?value),
  isImmutableHContext(?hctx).

VarPointsTo(?hctx, ?value, ?callerCtx, ?return):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  !(isComprehensionFunction(?tofunction)),   //TODO: REVIEW THE PYTHON IR TO AVOID THIS UGLY HACK
  AssignInstruction_To(?invo, ?return),
  isReturnNonNone_Insn(?retInsn),
  Instruction_Function(?retInsn, ?tofunction),
  ReturnNonNone_Var(?retInsn, ?var),
  VarPointsTo(?hctx, ?value, ?calleeCtx, ?var).