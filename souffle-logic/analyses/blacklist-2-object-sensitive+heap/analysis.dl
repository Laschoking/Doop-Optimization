// Context-sensitive pointer analysis with context of 2 object and a
// context-sensitive heap abstraction.

#include "../../main/single-phase-analysis.dl"
#include "../../main/configuration.dl"

// For this analysis the context of a method call corresponds to the
// identity of the receiver object and the receiver object of the caller.
// Again, this may trigger creation of a new object.
.comp BlacklistTwoObjectSensitivePlusHeapConfiguration : AbstractConfiguration {

  // In this analysis, the real context is a pair of Values and the real
  // heap context a single Value.
  .type Context  = [ value1:Value, value2:Value ]
  .type HContext = [ value:Value ]

  .decl BlacklistedHeapAllocation(value: Value)

  BlacklistedHeapAllocation("<java.util.HashSet: void <init>()>/new java.util.HashMap/0").
  BlacklistedHeapAllocation("<java.util.HashSet: void <init>(int)>/new java.util.HashMap/0").
  BlacklistedHeapAllocation("<java.util.HashSet: void <init>(int,float)>/new java.util.HashMap/0").
  BlacklistedHeapAllocation("<java.util.HashSet: void <init>(java.util.Collection)>/new java.util.HashMap/0").
  BlacklistedHeapAllocation("<java.util.HashMap: java.util.HashMap$TreeNode newTreeNode(int,java.lang.Object,java.lang.Object,java.util.HashMap$Node)>/new java.util.HashMap$TreeNode/0").
  BlacklistedHeapAllocation("<java.util.HashMap: java.util.HashMap$TreeNode replacementTreeNode(java.util.HashMap$Node,java.util.HashMap$Node)>/new java.util.HashMap$TreeNode/0").

  ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?tomethod, ?calleeCtx) :-
    ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?tomethod, _),
    ?calleeCtx = [?callerHctxValue, ?hctxValue],
    ?hctx = [?hctxValue],
    ?callerCtx = [?callerHctxValue, ?callerValue],
    ?callerValue = ?callerValue,
    BlacklistedHeapAllocation(?value).

  ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?tomethod, ?calleeCtx) :-
    ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?tomethod, _),
    ?hctx = [?hctxValue],
    ?calleeCtx = [?hctxValue, ?value],
   !BlacklistedHeapAllocation(?value).

  StaticContextResponse(?callerCtx, ?invo, ?calleeCtx) :-
    StaticContextRequest(?callerCtx, ?invo),
    ?calleeCtx = ?callerCtx.

  RecordContextResponse(?ctx, ?value, ?var, ?hctx) :-
    RecordContextRequest(?ctx, ?value, ?var),      
    ?ctx = [?ctxValue1, ?ctxValue2],
    ?hctx = [?ctxValue2],
    ?ctxValue1 = ?ctxValue1.

  InitContextResponse(?value, ?ctx) :-
    InitContextRequest(?value),
    ?ctx = [?value, ?value].

  InitHContextResponse(?value, ?hctx) :-
    InitHContextRequest(?value),
    ?hctx = [?value].

#ifdef HEAPDLS
  DynamicContextToContext([?value1, ?value2], ?dynCtx) :-
    DynamicContextHeap1(?heap1, ?dynCtx),
    Value_byDynamicHeap(?heap1, ?value1),
    DynamicContextHeap2(?heap2, ?dynCtx),
    Value_byDynamicHeap(?heap2, ?value2).

  DynamicContextToContext([?value, ?any], ?dynCtx) :-
    DynamicContextHeap1(?heap1, ?dynCtx),
    Value_byDynamicHeap(?heap1, ?value),
    !DynamicContextHeap2(_, ?dynCtx),
    isContext([?value, ?any]).

  DynamicContextToContext(?ctx, ?dynCtx) :-
    isDynamicContext(?dynCtx),
    !DynamicContextHeap1(_, ?dynCtx),
    isImmutableContext(?ctx).

  DynamicContextToHContext([?value], ?dynCtx) :-
    Value_byDynamicHeap(?heap1, ?value), 
    DynamicContextHeap1(?heap1, ?dynCtx).

  DynamicContextToHContext(?hctx, ?dynCtx) :-
    isDynamicContext(?dynCtx),
    !DynamicContextHeap1(_, ?dynCtx),
    isImmutableHContext(?hctx).
#endif // HEAPDLS

}
