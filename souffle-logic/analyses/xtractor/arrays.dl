#pragma once
#include "expr-values.dl"

.decl META_ArrayInfo(array: Var, name: symbol, types: symbol)
.decl META_ArraySizes(array: Var, dim: number, size: number)
.decl META_ArrayLoad(to: Var, from: Var, index: number)
.decl META_ArrayStore(to: Var, index: number, value: number)

.output META_ArrayInfo
.output META_ArraySizes
.output META_ArrayLoad
.output META_ArrayStore

META_ArrayInfo(array, name, type) :-
	InterestingStmt(stmt),
	AssignInstruction_To(stmt, array),
	Var_Type(array, type),
	Var_SimpleName(array, name),
	isArrayType(type),
	!StoreArrayIndex_From(_, array).

META_ArraySizes(array, 0, size) :-
	InterestingStmt(stmt),
	AssignInstruction_To(stmt, array),
	IVALUE_ArraySize_EXT(stmt, size).

META_ArraySizes(baseArray, dim + 1, size) :-
	InterestingStmt(stmt),
	StoreArrayIndex_Base(stmt, baseArray),
	StoreArrayIndex_From(stmt, array),
	META_ArraySizes(array, dim, size).

META_ArraySizes(otherArray, dim, size) :-
	META_ArraySizes(array, dim, size),
	Flows(array, otherArray).

// to = from[index]
// Due to SSA, `to` will appear only once
META_ArrayLoad(to, from, index) :-
	InterestingStmt(stmt),
	LoadArrayIndex_To(stmt, to),
	LoadArrayIndex_Base(stmt, from),
	IVALUE_ArrayIndex_EXT(stmt, index).

// to[index] = value
META_ArrayStore(to, index, value) :-
	InterestingStmt(stmt),
	StoreArrayIndex_Base(stmt, to),
	IVALUE_ArrayIndex_EXT(stmt, index),
	StoreArrayIndex_From(stmt, valueVar),
	IVALUE_Var(valueVar, value).
