#pragma once
#include "arrays.dl"
#include "conditions.dl"
#include "common.dl"

.decl LocalBlockEdge(fromBB: Instruction, toBB: Instruction)
.decl StructuredLoopBackEdge(fromBB: Instruction, toBB: Instruction)
.decl StructuredloopHead(bb: Instruction)
.decl BlockInStructuredLoop(bb: Instruction, loopHead: Instruction)
.decl StmtInStructuredLoop(stmt: Instruction, loopHead: Instruction)
.decl ContainsInnerStructuredLoop(loopHead: Instruction, innerloopHead: Instruction)
.decl InnermostStructuredLoop(loopHead: Instruction)
// A variable that is monotonically changes with each successive iteration in loop
.decl LoopInitialValue(condVar: Var, value: number, loopHead: Instruction)
.decl ChangeByOne(stmt: Instruction, to: Var, var: Var)
.decl InductionVariable(var: Var, loopHead: Instruction)
.decl LoopMainExitCond(jumpStmt: Instruction)
.decl LoopExitCond_Stmt(jumpStmt: Instruction, loopHead: Instruction)
.decl LoopExitCond(condVar: Var, op: symbol, rhs: number, loopHead: Instruction)
.decl OUT_ArrayUsedInLoop(array: Var, indexVar: Var, loopHead: Instruction)
.decl OUT_ArrayDimIterated(array: Var, dim: number, loopHead: Instruction)

.output LoopInitialValue
.output InductionVariable
.output LoopExitCond
.output OUT_ArrayUsedInLoop
.output OUT_ArrayDimIterated

LocalBlockEdge(fromBB, toBB) :-
	InterestingStmt(fromBB),
	BasicBlockTail(fromBB, endStmt),
	MaySuccessorModuloThrow(toBB, endStmt).

StructuredLoopBackEdge(fromBB, toBB),
StructuredloopHead(toBB) :-
	InterestingStmt(fromBB),
	LocalBlockEdge(fromBB, toBB),
	Dominates(toBB, fromBB).

BlockInStructuredLoop(backEdge, loopHead) :-
	StructuredLoopBackEdge(backEdge, loopHead).

BlockInStructuredLoop(loopHead, loopHead) :-
	StructuredloopHead(loopHead).

BlockInStructuredLoop(block, loopHead) :-
	BlockInStructuredLoop(other, loopHead),
	LocalBlockEdge(block, other),
	other != loopHead.

StmtInStructuredLoop(stmt, loopHead) :-
	BlockInStructuredLoop(block, loopHead),
	BasicBlockHead(stmt, block).

ContainsInnerStructuredLoop(loopHead, innerloopHead) :-
	StructuredloopHead(loopHead),
	StructuredloopHead(innerloopHead),
	BlockInStructuredLoop(innerloopHead, loopHead),
	loopHead != innerloopHead.

InnermostStructuredLoop(loopHead) :-
	StructuredloopHead(loopHead),
	!ContainsInnerStructuredLoop(loopHead, _).


// var + 1
ChangeByOne(stmt, to, var) :-
	BINOP(stmt, to, "+"), OPERAND_VAR(stmt, 1, var), IVALUE_OPERAND(stmt, 2, 1).
// 1 + var
ChangeByOne(stmt, to, var) :-
	BINOP(stmt, to, "+"), OPERAND_VAR(stmt, 2, var), IVALUE_OPERAND(stmt, 1, 1).
// var - (-1)
ChangeByOne(stmt, to, var) :-
	BINOP(stmt, to, "-"), OPERAND_VAR(stmt, 1, var), IVALUE_OPERAND(stmt, 2, -1).
// var - 1
ChangeByOne(stmt, to, var) :-
	BINOP(stmt, to, "-"), OPERAND_VAR(stmt, 1, var), IVALUE_OPERAND(stmt, 2, 1).
// var + (-1)
ChangeByOne(stmt, to, var) :-
	BINOP(stmt, to, "+"), OPERAND_VAR(stmt, 1, var), IVALUE_OPERAND(stmt, 2, -1).

InductionVariable(inductionVar, loopHead) :-
	StmtInStructuredLoop(stmt, loopHead),
	ChangeByOne(stmt, to, var),
	Flows(to, var),
	Flows(to, inductionVar).

// If statement has one edge jumping inside the loop, and another outside of it
LoopExitCond_Stmt(jumpStmt, loopHead) :-
	BlockInStructuredLoop(jumpBlock, loopHead),
	BasicBlockTail(jumpBlock, jumpStmt),
	IF(jumpStmt, _, _),
	LocalBlockEdge(jumpBlock, out),
	!BlockInStructuredLoop(out, loopHead),
	LocalBlockEdge(jumpBlock, in),
	BlockInStructuredLoop(in, loopHead).

LoopMainExitCond(stmt) :-
	StructuredloopHead(loopHead),
	BasicBlockTail(loopHead, stmt),
	IF(stmt, _, _).

LoopExitCond(condVar, reverseOp, value, loopHead) :-
	LoopExitCond_Stmt(stmt, loopHead),
	IF(stmt, op, _),
	ReverseCondition(op, reverseOp),
	OPERAND_VAR(stmt, 1, condVar),
	InductionVariable(condVar, loopHead),
	IVALUE_OPERAND(stmt, 2, value).

LoopExitCond(condVar, flippedOp, value, loopHead) :-
	LoopExitCond_Stmt(stmt, loopHead),
	IF(stmt, op, _),
	ReverseCondition(op, reverseOp),
	FlippedCondition(reverseOp, flippedOp),
	OPERAND_VAR(stmt, 2, condVar),
	InductionVariable(condVar, loopHead),
	IVALUE_OPERAND(stmt, 1, value).

LoopInitialValue(condVar, value, loopHead) :-
	LoopExitCond(condVar, _, _, loopHead),
	StmtInStructuredLoop(stmt, loopHead),
	MOVE(stmt, condVar, from),
	IVALUE_Var(from, value).

OUT_ArrayUsedInLoop(array, indexVar, loopHead) :-
	StmtInStructuredLoop(stmt, loopHead),
	(ALOAD_VAR(stmt, _, array, indexVar) ;
	 ASTORE_VAR(stmt, array, indexVar, _)),
	InductionVariable(indexVar, loopHead).

OUT_ArrayUsedInLoop(origArray, indexVar, loopHead) :-
	OUT_ArrayUsedInLoop(array, indexVar, loopHead),
	Flows(origArray, array).

IVALUE_Var(to, size) :-
	InterestingStmt(stmt),
	UNOP(stmt, to, "len"),
	OPERAND_VAR(stmt, 1, array),
	OUT_ArrayDims(array, 0, size).

// [0..size)
OUT_ArrayDimIterated(array, 0, loopHead) :-
	OUT_ArrayUsedInLoop(array, indexVar, loopHead),
	InductionVariable(indexVar, loopHead),
	LoopInitialValue(indexVar, 0, loopHead),
	OUT_ArrayDims(array, 0, size),
	(LoopExitCond(indexVar, "<", size, loopHead);
	 LoopExitCond(indexVar, "<=", size - 1, loopHead)).

// (size..0]
OUT_ArrayDimIterated(array, 0, loopHead) :-
	OUT_ArrayUsedInLoop(array, indexVar, loopHead),
	InductionVariable(indexVar, loopHead),
	OUT_ArrayDims(array, 0, size),
	LoopInitialValue(indexVar, size - 1, loopHead),
	LoopExitCond(indexVar, ">=", 0, loopHead).

// toArray = baseArray[..]
OUT_ArrayDimIterated(baseArray, dim + 1, loopHead) :-
	OUT_ArrayDimIterated(toArray, dim, loopHead),
	(ALOAD_VAR(_, toArray, baseArray, _) ;
	 ALOAD_NUM(_, toArray, baseArray, _)).

OUT_ArrayDimIterated(baseArray, dim + 1, loopHead) :-
	OUT_ArrayDimIterated(toArray, dim, loopHead),
	(ALOAD_VAR(stmt, toArray, baseArray, _) ;
	 ALOAD_NUM(stmt, toArray, baseArray, _)).