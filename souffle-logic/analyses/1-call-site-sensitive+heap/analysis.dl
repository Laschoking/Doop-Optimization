// Context-sensitive pointer analysis with context of 1 call site and a
// context-sensnitive heap abstraction of 1 call site.

#include "../../main/single-phase-analysis.dl"
#include "../../main/configuration.dl"

.comp OneCallSiteSensitivePlusHeapConfiguration : AbstractConfiguration {
  // In this analysis, both the real context and the real heap context are
  // MethodInvocations.
  .type Context  = [ invocation:MethodInvocation ]
  .type HContext = [ invocation:MethodInvocation ]

  ContextResponse(?callerCtx, ?hctx, ?invo, ?value, ?tomethod, ?calleeCtx) :-
    ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?tomethod, _),
    ?calleeCtx = [?invo].

  StaticContextResponse(?callerCtx, ?invo, ?calleeCtx) :-
    StaticContextRequest(?callerCtx, ?invo),
    ?calleeCtx = [?invo].

  RecordContextResponse(?ctx, ?value, ?var, ?hctx) :-
    RecordContextRequest(?ctx, ?value, ?var),      
    ?ctx = [?invo],
    ?hctx = [?invo].

  InitContextResponse(?invocation, ?ctx) :-
    InitContextRequest(?invocation),
    ?ctx = [?invocation].

  InitHContextResponse(?value, ?hctx) :-
    InitHContextRequest(?value),
    ?hctx = [?value].

  .decl StartupInvocation(?invocation:MethodInvocation)

  StartupInvocation("<<jvm-startup-context>>").

  /**
   * Special calling contexts
   */

  // PlusMethodInvocation macro should be used here.
  // However, it is not as souffle reports syntax error
  // if we try to assert two facts at once, e.g.,
  // isInstuction("foo"), isMethodInvocation("foo").
  isInstruction("<<initial-context>>").
  isMethodInvocation("<<initial-context>>").

  isInstruction("<<jvm-startup-context>>").
  isMethodInvocation("<<jvm-startup-context>>").

  isInstruction("<<jvm-startup-hcontext>>").
  isMethodInvocation("<<jvm-startup-hcontext>>").

  isInstruction("<<clinit-context>>").
  isMethodInvocation("<<clinit-context>>").

  isInstruction("<<immutable-context>>").
  isMethodInvocation("<<immutable-context>>").

  isInstruction("<<immutable-hcontext>>").
  isMethodInvocation("<<immutable-hcontext>>").
}
