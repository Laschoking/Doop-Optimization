// Axioms and inference rules for boolean ?expressions

// Every ?expression implies itself

ExprImpliesOther(?expr1, ?expr1) :-
  isBooleanExpr(?expr1).

//
// A && B => A and A && B => B
//

ExprImpliesOther(?expr, ?exprLeft),
ExprImpliesOther(?expr, ?exprRight) :-
  isBooleanExprLeftRight(?expr, ?exprLeft, ?exprRight, "&&").
//
// A && B => B && A
//

ExprImpliesOther(?expr, ?exprOther) :-
  isBooleanExprLeftRight(?expr, ?exprLeft, ?exprRight, "&&"),
  isBooleanExprLeftRight(?exprOther, ?exprRight, ?exprLeft, "&&").

//
// A => A || B and B => A || B
//

ExprImpliesOther(?exprLeft, ?expr),
ExprImpliesOther(?exprRight, ?expr) :-
  isBooleanExprLeftRight(?expr, ?exprLeft, ?exprRight, "||").
//
// A || B => B || A
//

ExprImpliesOther(?expr, ?exprOther) :-
  isBooleanExprLeftRight(?expr, ?exprLeft, ?exprRight, "||"),
  isBooleanExprLeftRight(?exprOther, ?exprRight, ?exprLeft, "||").

/* Distributive property rules */

//
// A && (B || C) => (A && B) || (A && C)
//

isExpr(?exprOther),
isExpr(?exprLeft),
isExpr(?exprRight),
ExprImpliesOther(?expr, ?exprOther) :-
  isBooleanExprLeftRight(?expr, ?exprA, ?exprOr, "&&"),
  ?exprOr = ["||", "boolean", ?exprB, ?exprC],
  ?exprLeft = ["&&", "boolean", ?exprA, ?exprB],
  ?exprRight = ["&&", "boolean", ?exprA, ?exprC],
  ?exprOther = ["||", "boolean", ?exprLeft, ?exprRight].

//
// (A && B) || (A && C) => A && (B || C)
//

isExpr(?exprOther),
isExpr(?exprNRight),
ExprImpliesOther(?expr, ?exprOther) :-
  isBooleanExprLeftRight(?expr, ?exprLeft, ?exprRight, "||"),
  ?exprLeft = ["&&", "boolean", ?exprA, ?exprB],
  ?exprRight = ["&&", "boolean", ?exprA, ?exprC],
  ?exprNRight =["||", "boolean", ?exprB, ?exprC],
  ?exprOther = ["&&", "boolean", ?exprA, ?exprNRight].

//
// A || (B && C) => (A || B) && (A || C)
//

ExprImpliesOther(?expr, ?exprOther) :-
  ?exprFoo = ["&&", "boolean", ?exprB, ?exprC],
  isBooleanExprLeftRight(?expr, ?exprA, ?exprFoo, "||"),
  ?exprLeft = ["||", "boolean", ?exprA, ?exprB],
  ?exprRight = ["||", "boolean", ?exprA, ?exprC],
  isBooleanExprLeftRight(?exprOther, ?exprLeft, ?exprRight, "&&").

//
// (A || B) && (A || C) => A || (B && C)
//

ExprImpliesOther(?expr, ?exprOther) :-
  ?expr1 = ["||", "boolean", ?exprA, ?exprB],
  ?expr2 = ["||", "boolean", ?exprA, ?exprC],
  isBooleanExprLeftRight(?expr, ?expr1, ?expr2, "&&"),
  ?exprRight = ["&&", "boolean", ?exprB, ?exprC],
  isBooleanExprLeftRight(?exprOther, ?exprA, ?exprRight, "||").


/////////// ===========================================
/// Above are axioms, below are inference rules
///////////

//
// A => B and B => C implies A => C
//

ExprImpliesOther(?expr, ?exprOther) :-
  ExprImpliesOther(?expr, ?exprInter),
  ExprImpliesOther(?exprInter, ?exprOther).
 .plan 1:(2,1)

//
//// A => B and A => C implies A => B && C
//

ExprImpliesOther(?exprA, ?exprOther) :-
  isBooleanExprLeftRight(?exprOther, ?exprB, ?exprC, "&&"),
  ExprImpliesOther(?exprA, ?exprB),
  ExprImpliesOther(?exprA, ?exprC).
 .plan 1:(1,3,2)

//
//// B => A and C => A implies B || C => A
//

ExprImpliesOther(?exprLeft, ?exprA) :-
  isBooleanExprLeftRight(?exprLeft, ?exprB, ?exprC, "||"),
	ExprImpliesOther(?exprB, ?exprA),
  ExprImpliesOther(?exprC, ?exprA).
 .plan 1:(1,3,2)

//
//// A => B and ExprIsNegationOfOther(B, B') and ExprIsNegationOfOther(A, A') implies B' => A'
//

ExprImpliesOther(?exprBP, ?exprAP) :-
  ExprImpliesOther(?exprA, ?exprB),
  ExprIsNegationOfOther(?exprB, ?exprBP),
  ExprIsNegationOfOther(?exprA, ?exprAP).

//
//// ExprIsAlwaysFalse(B) implies B => A
//

ExprImpliesOther(?expr, ?exprOther) :-
	ExprIsAlwaysFalse(?expr),
	isBooleanExpr(?exprOther).

//
//// ExprIsAlwaysTrue(B) implies A => B
//

ExprImpliesOther(?exprOther, ?expr) :-
	ExprIsAlwaysTrue(?expr),
	isBooleanExpr(?exprOther).

//
//// A => (B || false)  implies  A => B
//

ExprImpliesOther(?expr, ?exprOther) :-
  ExprImpliesOther(?expr, ?exprCompOther),
  ?exprCompOther = ["||", ?type, ?exprOther, ?exprFalse],
  ExprIsAlwaysFalse(?exprFalse).

//
//// (Î‘ && true) => B implies A => B
//

ExprImpliesOther(?expr, ?exprOther) :-
  ExprImpliesOther(?exprCompOther, ?exprOther),
  ?exprCompOther = ["&&", ?type, ?expr, ?exprTrue],
  ExprIsAlwaysTrue(?exprTrue).

//
//// ExprIsNegationOfOther
//

ExprIsNegationOfOther(?expr, ?exprOther) :-
  Expression_Method(?expr, ?meth),
  Expression_Method(?exprOther, ?meth),
  NegateOp(?op, ?negatedOp),
  ?expr = [?op, ?type, ?exprLeft, ?exprRight],
  ?exprOther = [?negatedOp, ?type, ?exprLeft, ?exprRight].

ExprIsNegationOfOther(?expr, ?exprOther) :-
	?expr = ["1", "boolean", nil, nil],
 	?exprOther = ["0", "boolean", nil, nil],
	isBooleanExpr(?expr),
	isBooleanExpr(?expr).

//
//// ExprIsAlwaysTrue
//

ExprIsAlwaysTrue(?expr) :-
	isBooleanExpr(?expr),
	?expr = ["1", "boolean", nil, nil].

//
//// ExprIsAlwaysFalse
//

ExprIsAlwaysFalse(?expr) :-
	isBooleanExpr(?expr),
	?expr = ["0", "boolean", nil, nil].

