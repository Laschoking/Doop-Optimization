// Axioms and inference rules for boolean ?expressions

// Every ?expression implies itself

ExprImpliesOther(?expr1, ?expr1) :-
  isBooleanExpr(?expr1).

//
// A && B => A and A && B => B
//

ExprImpliesOther(?expr, ?left),
ExprImpliesOther(?expr, ?right) :-
  ?expr = ["&&", "boolean", ?left, ?right],
  isBooleanExpr(?expr).

//
// A && B => B && A
//

ExprImpliesOther(?expr, ?exprOther) :-
  ?expr = ["&&", "boolean", ?left, ?right],
  isBooleanExpr(?expr),
  ?exprOther = ["&&", "boolean", ?right, ?left].

//
// A => A || B and B => A || B
//

ExprImpliesOther(?left, ?expr),
ExprImpliesOther(?right, ?expr) :-
  ?expr = ["||", "boolean", ?left, ?right],
  isBooleanExpr(?expr).

//
// A || B => B || A
//

ExprImpliesOther(?expr, ?exprOther) :-
  ?expr = ["||", "boolean", ?left, ?right],
  isBooleanExpr(?expr),
  ?exprOther = ["||", "boolean", ?right, ?left].

/* Distributive property rules */

//
// A && (B || C) => (A && B) || (A && C)
//

isExpr(?exprOther),
isExpr(?exprLeft),
isExpr(?exprRight),
ExprImpliesOther(?expr, ?exprOther) :-
  ?expr = ["&&", "boolean", ?exprA, ?exprOr],
  isBooleanExpr(?expr),
  ?exprOr = ["||", "boolean", ?exprB, ?exprC],
  ?exprLeft = ["&&", "boolean", ?exprA, ?exprB],
  ?exprRight = ["&&", "boolean", ?exprA, ?exprC],
  ?exprOther = ["||", "boolean", ?exprLeft, ?exprRight].

//
// (A && B) || (A && C) => A && (B || C)
//

isExpr(?exprOther),
isExpr(?exprNRight),
ExprImpliesOther(?expr, ?exprOther) :-
  ?expr = ["||", "boolean", ?exprLeft, ?exprRight],
  isBooleanExpr(?expr),
  ?exprLeft = ["&&", "boolean", ?exprA, ?exprB],
  ?exprRight = ["&&", "boolean", ?exprA, ?exprC],
  ?exprNRight =["||", "boolean", ?exprB, ?exprC],
  ?exprOther = ["&&", "boolean", ?exprA, ?exprNRight].

//
// A || (B && C) => (A || B) && (A || C)
//

ExprImpliesOther(?expr, ?exprOther) :-
  ?expr = ["||", "boolean", ?exprA, ["&&", "boolean", ?exprB, ?exprC]],
  isBooleanExpr(?expr),
  ?exprLeft = ["||", "boolean", ?exprA, ?exprB],
  ?exprRight = ["||", "boolean", ?exprA, ?exprC],
  ?exprOther = ["&&", "boolean", ?exprLeft, ?exprRight].

//
// (A || B) && (A || C) => A || (B && C)
//

ExprImpliesOther(?expr, ?exprOther) :-
  ?expr = ["&&", "boolean", ["||", "boolean", ?exprA, ?exprB], ["||", "boolean", ?exprA, ?exprC]],
  isBooleanExpr(?expr),
  ?exprRight = ["&&", "boolean", ?exprB, ?exprC],
  ?exprOther = ["||", "boolean", ?exprA, ?exprRight].


/////////// ===========================================
/// Above are axioms, below are inference rules
///////////

//
// A => B and B => C implies A => C
//

ExprImpliesOther(?expr, ?exprOther) :-
  ExprImpliesOther(?expr, ?exprInter),
  ExprImpliesOther(?exprInter, ?exprOther).
 .plan 1:(2,1)

//
//// A => B and A => C implies A => B && C
//

ExprImpliesOther(?exprA, ?exprOther) :-
  ?exprOther = ["&&", "boolean", ?exprB, ?exprC],
  isBooleanExpr(?exprOther),
  ExprImpliesOther(?exprA, ?exprB),
  ExprImpliesOther(?exprA, ?exprC).
 .plan 1:(1,3,2)

//
//// B => A and C => A implies B || C => A
//

ExprImpliesOther(?exprLeft, ?exprA) :-
  ?exprLeft = ["||", "boolean", ?exprB, ?exprC],
  isBooleanExpr(?exprLeft),
	ExprImpliesOther(?exprB, ?exprA),
  ExprImpliesOther(?exprC, ?exprA).
 .plan 1:(1,3,2)

//
//// A => B and ExprIsNegationOfOther(B, B') and ExprIsNegationOfOther(A, A') implies B' => A'
//

ExprImpliesOther(?exprBP, ?exprAP) :-
  ExprImpliesOther(?exprA, ?exprB),
  ExprIsNegationOfOther(?exprB, ?exprBP),
  ExprIsNegationOfOther(?exprA, ?exprAP).

//
//// ExprIsAlwaysFalse(B) implies B => A
//

ExprImpliesOther(?expr, ?exprOther) :-
	ExprIsAlwaysFalse(?expr),
	isBooleanExpr(?exprOther).

//
//// ExprIsAlwaysTrue(B) implies A => B
//

ExprImpliesOther(?exprOther, ?expr) :-
	ExprIsAlwaysTrue(?expr),
	isBooleanExpr(?exprOther).

//
//// A => (B || false)  implies  A => B
//

ExprImpliesOther(?expr, ?exprOther) :-
  ExprImpliesOther(?expr, ?exprCompOther),
  ?exprCompOther = ["||", ?type, ?exprOther, ?exprFalse],
  ExprIsAlwaysFalse(?exprFalse).

//
//// (Î‘ && true) => B implies A => B
//

ExprImpliesOther(?expr, ?exprOther) :-
  ExprImpliesOther(?exprCompOther, ?exprOther),
  ?exprCompOther = ["&&", ?type, ?expr, ?exprTrue],
  ExprIsAlwaysTrue(?exprTrue).

//
//// ExprIsNegationOfOther
//

ExprIsNegationOfOther(?expr, ?exprOther) :-
  Expression_Method(?expr, ?meth),
  Expression_Method(?exprOther, ?meth),
  NegateOp(?op, ?negatedOp),
  ?expr = [?op, ?type, ?exprLeft, ?exprRight],
  ?exprOther = [?negatedOp, ?type, ?exprLeft, ?exprRight].

ExprIsNegationOfOther(?expr, ?exprOther) :-
	?expr = ["1", "boolean", nil, nil],
 	?exprOther = ["0", "boolean", nil, nil],
	isBooleanExpr(?expr),
	isBooleanExpr(?expr).

//
//// ExprIsAlwaysTrue
//

ExprIsAlwaysTrue(?expr) :-
	isBooleanExpr(?expr),
	?expr = ["1", "boolean", nil, nil].

//
//// ExprIsAlwaysFalse
//

ExprIsAlwaysFalse(?expr) :-
	isBooleanExpr(?expr),
	?expr = ["0", "boolean", nil, nil].

