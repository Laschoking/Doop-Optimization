#pragma once
#include "macros.dl"
#include "declarations.dl"
#include "delta.dl"
#include "core.dl"

#ifdef ANDROID
#include "../../main/android-mocking.dl"
#endif // ANDROID

#define DUMMY_CONTEXT "-"

MethodInvocationInContext(DUMMY_CONTEXT, ?invocation, ?tomethod),
AnyMethodInvocation(?invocation, ?tomethod) :-
  ResolveCall(?invocation, ?tomethod).

TypeForReturnValue(?type, ?ret, ?invocation) :-
  AssignCast(?type, ?ret, _, _),
  MethodInvocationInfo(?invocation, _, ?ret).

#ifdef ANDROID
PlusMethodInvocation(?fakeNewSite),
ALLOC(?fakeNewSite, ?to, ?alloc),
HeapAllocation_Type(?alloc, ?type) :-
  MethodInvocation_Method(?invo, ?method),
  Method_SimpleName(?method, ?name),
  Method_Descriptor(?method, ?desc),
  Method_DeclaringType(?method, ?declType),
  basic.MethodLookup(?name, ?desc, ?declType, ?invokedMethod),
  AndroidMethodReturns(?invokedMethod, ?alloc, ?type),
  ?fakeNewSite = cat("<mock allocation site for ", cat(?alloc, ">")),
  AssignReturnValue(?invo, ?to).
#endif // ANDROID

// Variant of core rule, without the negation in AssignReturnValue.
ParamTaintTransferredToBase(?param, ?ctx, ?base) :-
  TaintTransferMethodInvocationInContext(?ctx, ?index, ?invocation),
  ActualParam(?index, ?invocation, ?param),
  MethodInvocation_Base(?invocation, ?base).

// Variable is tainted with label.
.decl Var_Taint(?v:Var, ?label:InformationLabel)
.output Var_Taint

// Base case: tainting method return value.
Var_Taint(?to, ?label) :-
  CallTaintingMethod(?label, _, ?invocation),
  AssignReturnValue(?invocation, ?to).
// Recursive case, based on core relation.
Var_Taint(?b, ?label) :-
  Var_Taint(?a, ?label),
  VarIsTaintedFromVar(_, _, ?b, _, ?a).
// Recursive case: data-flow propagates taint.
Var_Taint(?to, ?label),
VarIsTaintedFromVar(?type, DUMMY_CONTEXT, ?to, DUMMY_CONTEXT, ?from) :-
  Var_Taint(?from, ?label),
  Flows(?from, ?to),
  Var_Type(?to, ?type).
// Recursive case: parameter-taints-base.
Var_Taint(?base, ?label) :-
  Var_Taint(?param, ?label),
  ParamTaintTransferredToBase(?param, _, ?base).
// Finally, handle the sinks.
LeakingTaintedInformation(?sourceLabel, ?destLabel, DUMMY_CONTEXT, ?invocation, ?source) :-
  Var_Taint(?var, ?sourceLabel),
  LeakingSinkVariable(?destLabel, ?invocation, _, ?var),
  ?source = "-".
