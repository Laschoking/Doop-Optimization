plugins {
    id 'application'
    id 'groovy'
    id 'java-library'
}

mainClassName = 'org.clyze.doop.soot.Main'
version = null

repositories {
    mavenCentral()
    maven {
        name "soot-snapshot"
        url "https://soot-build.cs.uni-paderborn.de/nexus/repository/soot-snapshot"
    }
    maven {
        name "soot-release"
        url "https://soot-build.cs.uni-paderborn.de/nexus/repository/soot-release"
    }
}

dependencies {
    api project(path: ':generators:fact-generator-common', configuration: 'shadow')

    // implementation "ca.mcgill.sable:soot:4.0.0"
    implementation "org.soot-oss:soot:4.2.1"

    testImplementation "org.codehaus.groovy:groovy-all:${groovyVersion}"  // Groovy
    testImplementation "org.spockframework:spock-core:${spockVersion}"
}

//fatJar already exists
fatJar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from { configurations.compileClasspath.collect { it.directory ? it : zipTree(it) } }
    with jar
}

task installJar(type: Copy) {
    from fatJar.outputs.files.singleFile
    into("../../${resourcesDir}")
}
installJar.dependsOn fatJar

task uninstallJar() {
    doLast {
        def jar = fatJar.outputs.files.singleFile
        if (jar && jar.name) { delete "../../${resourcesDir}/${jar.name}" }
    }
}

test {
    useJUnitPlatform()
}
