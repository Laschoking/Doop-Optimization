plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java-library'
}

String asmVersion = '8.0.1'

// WARNING: dependencies below should be minimal, since every
// dependency will be included as-is *in each* fact generator "fat" JAR.
dependencies {
    api("org.clyze:clue-common:${clueCommonVersion}") {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'org.apache.ivy', module: 'ivy'
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    api "org.clyze:metadata-model:${metadataVersion}"
    implementation "org.ow2.asm:asm-tree:${asmVersion}",    // Java bytecode library
                   "org.ow2.asm:asm-util:${asmVersion}"
    implementation 'org.smali:dexlib2:2.4.0'                // Dexlib2: library for reading/modifying/writing Android dex files
    implementation 'net.dongliu:apk-parser:2.6.10'          // needed for reading binary XML entries from APK inputs
    implementation "org.antlr:antlr4-runtime:${antlrVersion}"
    api 'org.clyze:native-scanner:0.6.6'
    api "log4j:log4j:${log4jVersion}"            // Logging implementation
    implementation 'org.apache.commons:commons-collections4:4.1'
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
}

// The Shadow JAR plugin ensures that the dexlib dependency does not
// pollute the namespace for the fact generators or other code using
// Doop as a library.
shadowJar {
    configurations = [project.configurations.compileClasspath]
    dependencies {
        exclude(dependency('com.google.code.gson:gson'))
        exclude(dependency('org.apache.ivy:ivy'))
        exclude(dependency('org.codehaus.groovy:groovy-all'))
    }
    relocate 'org.jf', 'org.clyze.doop.common.org.jf'
}
